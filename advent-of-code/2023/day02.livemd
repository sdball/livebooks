# Advent of Code 2023 - Day 2

```elixir
Mix.install([
  {:kino, "~> 0.11.3"},
  {:kino_vega_lite, "~> 0.1.11"},
  {:kino_explorer, "~> 0.1.12"}
])
```

## Puzzle Input

```elixir
puzzle_input_kino = Kino.Input.textarea("Please paste the puzzle input:")
```

```elixir
puzzle_input = Kino.Input.read(puzzle_input_kino)
```

## Part 1

The Elf shows you a small bag and some cubes which are either red, green, or blue. Each time you play this game, he will hide a secret number of cubes of each color in the bag, and your goal is to figure out information about the number of cubes.

To get information, once a bag has been loaded with cubes, the Elf will reach into the bag, grab a handful of random cubes, show them to you, and then put them back in the bag. He'll do this a few times per game.

You play several games and record the information from each game (your puzzle input). Each game is listed with its ID number (like the 11 in Game 11: ...) followed by a semicolon-separated list of subsets of cubes that were revealed from the bag (like 3 red, 5 green, 4 blue).

```elixir
part_1_test_input = """
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
"""
```

In game 1, three sets of cubes are revealed from the bag (and then put back again). The first set is 3 blue cubes and 4 red cubes; the second set is 1 red cube, 2 green cubes, and 6 blue cubes; the third set is only 2 green cubes.

The Elf would first like to know which games would have been possible if the bag contained **only 12 red cubes, 13 green cubes, and 14 blue cubes**?

In the example above, games 1, 2, and 5 would have been possible if the bag had been loaded with that configuration. However, game 3 would have been impossible because at one point the Elf showed you 20 red cubes at once; similarly, game 4 would also have been impossible because the Elf showed you 15 blue cubes at once. If you add up the IDs of the games that would have been possible, you get 8.

```elixir
defmodule Day02.Part1 do
  @rules %{
    :red => 12,
    :green => 13,
    :blue => 14
  }

  def parse_input_to_games(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&Day02.Part1.Game.parse/1)
  end

  def reject_invalid_games(games) do
    games
    |> Enum.reject(fn [_id, rounds] ->
      Enum.any?(rounds, &invalid_round?/1)
    end)
  end

  def invalid_round?(round) do
    Enum.any?(round, fn {count, color} ->
      count > Map.get(@rules, color)
    end)
  end

  def game_ids(games) do
    games
    |> Enum.map(fn [id, _rounds] -> id end)
  end

  defmodule Game do
    def parse(line) do
      line
      |> String.split(": ")
      |> Enum.map(fn
        "Game " <> id ->
          String.to_integer(id)

        rounds ->
          rounds
          |> String.split("; ")
          |> Enum.map(fn round ->
            round
            |> String.split(", ")
            |> Enum.map(fn pull ->
              [count, color] = String.split(pull)
              count = String.to_integer(count)
              color = String.to_atom(color)
              {count, color}
            end)
          end)
      end)
    end
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
part_1_test_input
|> Day02.Part1.parse_input_to_games()
|> Day02.Part1.reject_invalid_games()
|> Day02.Part1.game_ids()
|> Enum.sum()
```

```elixir
puzzle_input
|> Day02.Part1.parse_input_to_games()
|> Day02.Part1.reject_invalid_games()
|> Day02.Part1.game_ids()
|> Enum.sum()
```

## Part 2

As you continue your walk, the Elf poses a second question: in each game you played, **what is the fewest number of cubes of each color** that could have been in the bag to make the game possible?

```elixir
part_2_test_input = """
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
"""
```

```elixir
part_2_test_input
|> String.split("\n", trim: true)
```

In game 1, the game could have been played with as few as 4 red, 2 green, and 6 blue cubes. If any color had even one fewer cube, the game would have been impossible.

Game 2 could have been played with a minimum of 1 red, 3 green, and 4 blue cubes.

Game 3 must have been played with at least 20 red, 13 green, and 6 blue cubes.

Game 4 required at least 14 red, 3 green, and 15 blue cubes.

Game 5 needed no fewer than 6 red, 3 green, and 2 blue cubes in the bag.

The power of a set of cubes is equal to the numbers of red, green, and blue cubes multiplied together. The power of the minimum set of cubes in game 1 is 48. In games 2-5 it was 12, 1560, 630, and 36, respectively. Adding up these five powers produces the sum 2286.

For each game, find the minimum set of cubes that must have been present. What is the sum of the power of these sets?

```elixir
defmodule Day02.Part2 do
  def parse_input_to_games(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&Day02.Part2.Games.parse/1)
  end

  def reduce_games_to_minimum_cubes(games) do
    games
    |> Enum.map(&reduce_game_to_minimum_cubes/1)
  end

  def reduce_game_to_minimum_cubes(game) do
    game
    |> List.flatten()
    |> Enum.reduce(%{}, fn {count, color}, acc ->
      if count > Map.get(acc, color, 0) do
        Map.put(acc, color, count)
      else
        acc
      end
    end)
  end

  def calculate_cube_powers(cube_sets) do
    cube_sets
    |> Enum.map(fn cubes ->
      Map.values(cubes) |> Enum.reduce(&*/2)
    end)
  end

  defmodule Games do
    def parse(line) do
      [_id, rounds] = String.split(line, ": ")

      rounds
      |> String.split("; ")
      |> Enum.map(fn round ->
        round
        |> String.split(", ")
        |> Enum.map(fn pull ->
          [count, color] = String.split(pull)
          count = String.to_integer(count)
          color = String.to_atom(color)
          {count, color}
        end)
      end)
    end
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
part_2_test_input
|> Day02.Part2.parse_input_to_games()
|> Day02.Part2.reduce_games_to_minimum_cubes()
|> Day02.Part2.calculate_cube_powers()
|> Enum.sum()
```

```elixir
puzzle_input
|> Day02.Part2.parse_input_to_games()
|> Day02.Part2.reduce_games_to_minimum_cubes()
|> Day02.Part2.calculate_cube_powers()
|> Enum.sum()
```
